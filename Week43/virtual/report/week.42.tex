\documentclass[a4paper, titlepage]{article}

\usepackage{graphicx} % Required to insert images
\usepackage{natbib}
\usepackage{listings}

\usepackage{amsmath}
\usepackage{tabularx}
\usepackage[utf8]{inputenc}

\begin{document}

\title{Week 42}
\author{Sigurt Bladt Dinesen \\sidi{@}itu.dk \and Jens Egholm Pedersen \\jegp{@}itu.dk}
\maketitle
\section*{9.1}
\subsection*{9.1(i) Selsort.il}
\begin{lstlisting}[numbers=left, firstnumber=0, title=Selsort.il]
// method line 2
.method public static hidebysig 
       default void SelectionSort (int32[] arr)  cil managed 
{
// Method begins at RVA 0x20c0
// Code size 72 (0x48)
.maxstack 4
.locals init (
	int32	V_0,
	int32	V_1,
	int32	V_2,
	int32	V_3)
IL_0000:  ldc.i4.0                //push 4byte int 0
IL_0001:  stloc.0                 //store it locally
IL_0002:  br IL_003e              //jump to for guard

IL_0007:  ldloc.0                 //for body, load i
IL_0008:  stloc.1                 //store "to least"
IL_0009:  ldloc.0                 //load i again
IL_000a:  ldc.i4.1
IL_000b:  add 
IL_000c:  stloc.2                 //store i+1 (as j) for the inner loop
IL_000d:  br IL_0023              //jump to inner for

IL_0012:  ldarg.0                 //load arr
IL_0013:  ldloc.2                 //load j
IL_0014:  ldelem.i4               //load arr[j]
IL_0015:  ldarg.0                 //load arr
IL_0016:  ldloc.1                 //load least
IL_0017:  ldelem.i4               //load arr[least]
IL_0018:  bge IL_001f             //jmp to skip loop body if arr[j] > arr[least]

IL_001d:  ldloc.2
IL_001e:  stloc.1                 //store least = j
IL_001f:  ldloc.2
IL_0020:  ldc.i4.1
IL_0021:  add
IL_0022:  stloc.2                 //increment j
IL_0023:  ldloc.2                 //inner-for guard
IL_0024:  ldarg.0                 //load arr
IL_0025:  ldlen                   //replace with arr len
IL_0026:  conv.i4
IL_0027:  blt IL_0012             //jmp to <inner for body>, if len > arr[j]

IL_002c:  ldarg.0
IL_002d:  ldloc.0 
IL_002e:  ldelem.i4               //arr[i]
IL_002f:  stloc.3                 //saved to tmp
IL_0030:  ldarg.0
IL_0031:  ldloc.0 
IL_0032:  ldarg.0 
IL_0033:  ldloc.1 
IL_0034:  ldelem.i4               //arr[least]
IL_0035:  stelem.i4               //arr[i] =
IL_0036:  ldarg.0 
IL_0037:  ldloc.1 
IL_0038:  ldloc.3 
IL_0039:  stelem.i4               //arr[least] = tmp
IL_003a:  ldloc.0 
IL_003b:  ldc.i4.1 
IL_003c:  add 
IL_003d:  stloc.0                 //increment i
IL_003e:  ldloc.0                 //for guard, load i
IL_003f:  ldarg.0                 //push arr (array)
IL_0040:  ldlen                   //length of arr
IL_0041:  conv.i4
IL_0042:  blt IL_0007             //if enter, jmp to for body

IL_0047:  ret 
} // end of method Selsort::SelectionSort
\end{lstlisting}
\subsection*{9.1(ii) Selsort.jvmbytecode}
\begin{lstlisting}[numbers=left, firstnumber=0, title=Selsort.jvmbytecode]
  public static void SelectionSort(int[]); flags: ACC_PUBLIC, ACC_STATIC Code: stack=4, locals=4, args_size=1
0: iconst_0
1: istore_1                       //store 0
2: iload_1                        //for guard
3: aload_0                        //push arr
4: arraylength                    //push arr length
5: if_icmpge 57                   //jmp to halt if i > arr len
8: iload_1                        //push i
9: istore_2                       //save to least
10: iload_1                       //push i
11: iconst_1
12: iadd                          //i+1
13: istore_3                      //j=i+1
14: iload_3                       //inner-for guard
15: aload_0
16: arraylength
17: if_icmpge 37                  //if j > arrlen, jmp to <??>
20: aload_0                       //push arr
21: iload_3                       //and j
22: iaload                        //arr[j]
23: aload_0                       //arr
24: iload_2                       //least
25: iaload                        //arr[least]
26: if_icmpge 31                  //jmp to <??> if arr[j] > arr[least]
29: iload_3                       //push j       
30: istore_2                      //store to least
31: iinc 3, 1                     //j++
34: goto 14                       //jmp to inner-for guard
37: aload_0
38: iload_1
39: iaload                        //arr[i]
40: istore_3                      //tmp=arr[i]
41: aload_0
42: iload_1
43: aload_0
44: iload_2
45: iaload                        //arr[least]
46: iastore                       //arr[i] = arr[least]
47: aload_0
48: iload_2
49: iload_3
50: iastore                       //arr[least] = tmp
51: iinc 1, 1                     //i++
54: goto 2                        jmp to for guard
57: return
\end{lstlisting}
\end{document}
